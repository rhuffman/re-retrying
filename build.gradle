/*
 * Copyright 2012-2015 Ray Holder
 * Modifications copyright 2017 Robert Huffman
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.api.artifacts.maven.MavenDeployment


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Settings
ext {
    project_group = 'tech.huffman.re-retrying'
    project_version = '3.0.1-SNAPSHOT'
    project_jdk = '1.8'
    project_pom = {
        name 're-retrying'
        packaging 'jar'
        description 'A library to create configurable retrying strategies for an arbitrary function call, such as a method that connects to a remote service with flaky uptime.'
        url 'https://github.com/rhuffman/re-retrying'

        scm {
            url 'scm:git@github.com:rhuffman/re-retrying.git'
            connection 'scm:git@github.com:rhuffman/re-retrying.git'
            developerConnection 'scm:git@github.com:rhuffman/re-retrying.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'rhuffman'
                name 'Robert Huffman'
            }
        }
    }

    /** Function always returns a new manifest that can be customized */
    defaultManifest = {
        return manifest {
            def git_cmd = "git rev-parse HEAD"
            def git_proc = git_cmd.execute()
            attributes 'SCM-Revision': git_proc.text.trim()
            attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
            attributes 'Build-Host': InetAddress.localHost.hostName
        }
    }

    defaultBlank = { closure ->
        try {
            closure()
        } catch (MissingPropertyException e) {
            ''
        }
    }

}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Identifiers

group = project_group
version = project_version
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Plugins

apply plugin: 'java'
sourceCompatibility = project_jdk
targetCompatibility = project_jdk

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Dependencies

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:23.0'
    compileOnly 'com.google.code.findbugs:jsr305:2.0.2'

    // junit testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Artifacts

jar {
    doFirst {
        // Timestamp changes on every build. By evaluating it later, won't needlessly fail up-to-date checks.
        manifest = defaultManifest()
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
    doFirst {
        // Timestamp changes on every build. By evaluating it later, won't needlessly fail up-to-date checks.
        manifest = defaultManifest()
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    doFirst {
        // Timestamp changes on every build. By evaluating it later, won't needlessly fail up-to-date checks.
        manifest = defaultManifest()
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def sonatypeUser = getProperty('sonatypeUsername', 'SONATYPE_USERNAME_IS_NOT_SET')
            def sonatypePass = getProperty('sonatypePassword', 'SONATYPE_PASSWORD_IS_NOT_SET')

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            pom.project project_pom
        }
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Utilities

// enable Gradle Wrapper support for bootstrapping with specific version
task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

String getProperty(String name, String defaultValue) {
    project.hasProperty(name) ? project.getProperty(name) : defaultValue
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// IDE

idea {
    module {
        downloadSources = true
    }
    project {
        languageLevel = project_jdk
        vcs = 'Git'
    }
}
